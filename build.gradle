buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		maven {
			url "${artifactory_plugins_url}"
			credentials {
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: "org.springframework.boot"

group = 'com.jfrog'
version = addBuildNumberFromCI('0.2.0')

processResources {
	filesMatching("**/application.properties") {
		expand version: version
	}
}

jar {
	baseName = 'frogsws'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	maven {
		url "${artifactory_libs_url}"
		credentials {
			username = "${artifactory_user}"
			password = "${artifactory_password}"
		}
	}
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.codehaus.groovy:groovy')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

String addBuildNumberFromCI(String version) {
	def buildNumber = System.getenv().BUILD_NUMBER
	return buildNumber ? version + "-" + buildNumber : version
}
